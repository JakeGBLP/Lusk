import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.gradleup.shadow' version '8.3.0'
    id 'java'
}

compileJava {
    options.encoding = "UTF-8"
}

version = "1.3.10"

def java16 = 16
def java17 = 17
def java21 = 21

def oldestJava = java16
def latestJava = java21


repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://repo.codemc.io/repository/maven-snapshots/' }
    maven { url 'https://repo.skriptlang.org/releases' }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://jitpack.io/" }
}

dependencies {
    implementation('org.jetbrains:annotations:20.1.0')
    compileOnly("io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT")
    compileOnly('com.github.SkriptLang:Skript:2.12.1')
    implementation('org.bstats:bstats-bukkit:3.0.2')
    implementation('com.vdurmont:semver4j:3.1.0')
    implementation('net.wesjd:anvilgui:1.10.8-SNAPSHOT')
    shadow('junit:junit:3.8.2')

    implementation("it.jakegblp:nms-shade:1.0.0")
    implementation project(":version_adapter")
    implementation project(":skript_v2_6_4")
    implementation project(":skript_v2_7_3")
    implementation project(":skript_v2_8_7")
    implementation project(":skript_v2_9_5")
    implementation project(":skript_v2_10_0")

    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    testCompileOnly 'org.projectlombok:lombok:1.18.36'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'

    testRuntimeOnly 'com.google.code.gson:gson:2.11.0'
    testRuntimeOnly 'com.google.guava:guava:32.1.2-jre'
    testImplementation 'com.google.code.gson:gson:2.11.0'
    testImplementation 'com.google.guava:guava:32.1.2-jre'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(latestJava))
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['-source', '' + oldestJava, '-target', '' + oldestJava]
}

processResources {
    filter(ReplaceTokens, tokens: [version: version])
}

build {
    dependsOn shadowJar
    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs.add("-Xlint:unchecked")
        options.compilerArgs.add("-Xlint:deprecation")
    }
    doLast {
        subprojects.each { subproject ->
            delete subproject.layout.buildDirectory
        }
    }
}

shadowJar {
    archiveFileName = project.name + "-" + project.version + ".jar"
    relocate 'org.bstats', 'it.jakegblp.lusk.libs.bstats'
    relocate 'net.wesjd.anvilgui', 'it.jakegblp.lusk.libs.anvilgui'

    exclude 'it/jakegblp/lusk/test/**'
}

tasks.register('testShadowJar', ShadowJar) {
    archiveFileName = project.name + "-" + project.version + "-test.jar"
    from sourceSets.main.output // Main compiled classes
    from sourceSets.test.output // Test compiled classes and resources
    configurations = [project.configurations.testRuntimeClasspath] // Include test runtime dependencies

    relocate 'org.bstats', 'it.jakegblp.lusk.libs.bstats'
    relocate 'net.wesjd.anvilgui', 'it.jakegblp.lusk.libs.anvilgui'
    relocate 'com.google.common', 'it.jakegblp.lusk.libs.guava'
    relocate 'com.google.gson', 'it.jakegblp.lusk.libs.gson'
}

tasks.register('testBuild') {
    dependsOn testShadowJar
    description = "Runs a test build"
    group = "build"
    doLast {
        println "Test build completed: ${testShadowJar.archiveFile.get().asFile}"
    }
}

// Create a test task with given name, environments dir/file, dev mode and java version.
// -1 on the timeout means it'll be disabled.
void createTestTask(String name, String desc, String environments, int javaVersion, long timeout) {
    if (timeout == 0) {
        timeout = 300000 // 5 minutes
    }
    def artifact = 'build' + File.separator + 'libs' + File.separator + 'Lusk-nightly.jar'
    tasks.register(name, JavaExec) {
        description = desc
        dependsOn build
        javaLauncher = javaToolchains.launcherFor {
            languageVersion = JavaLanguageVersion.of(javaVersion)
        }
        group = 'execution'
        classpath = files([
                artifact,
                sourceSets.main.runtimeClasspath,
                configurations.testRuntimeClasspath
        ])
        mainClass = 'it.jakegblp.lusk.test.platform.PlatformMain'
        args = [
                'build/test_runners',
                'src/test/scripts',
                'src/test/resources/runner_data',
                environments,
                project.findProperty('verbosity') ?: "null",
                timeout
        ]
    }
}

String environments = 'src/test/environments/'
createTestTask('luskTest_1_16_5_skript_2_6_4', 'Runs tests on Paper 1.16.5 and with Skript 2.6.4.', environments + 'paper-1.16.5_skript-2.6.4.json', java16, -1)
createTestTask('luskTest_1_16_5_skript_2_7_3', 'Runs tests on Paper 1.16.5 and with Skript 2.7.3.', environments + 'paper-1.16.5_skript-2.7.3.json', java16, -1)
createTestTask('luskTest_1_16_5_skript_2_8_7', 'Runs tests on Paper 1.16.5 and with Skript 2.8.7.', environments + 'paper-1.16.5_skript-2.8.7.json', java16, -1)

createTestTask('luskTest_1_16_5', 'Runs tests on Paper 1.16.5.', environments + 'paper-1.16.5.json', java16, -1)
createTestTask('luskTest_1_17_1', 'Runs tests on Paper 1.17.1.', environments + 'paper-1.17.1.json', java17, -1)
createTestTask('luskTest_1_18_2', 'Runs tests on Paper 1.18.2.', environments + 'paper-1.18.2.json', java17, -1)
createTestTask('luskTest_1_19_4', 'Runs tests on Paper 1.19.4.', environments + 'paper-1.19.4.json', java17, -1)
createTestTask('luskTest_1_20_6', 'Runs tests on Paper 1.20.6.', environments + 'paper-1.20.6.json', java21, -1)
createTestTask('luskTest_1_21_4', 'Runs tests on Paper 1.21.4.', environments + 'paper-1.21.4.json', java21, -1)
createTestTask('luskTest_1_21_5', 'Runs tests on Paper 1.21.5.', environments + 'paper-1.21.5.json', java21, -1)
createTestTask('luskTest_1_21_6', 'Runs tests on Paper 1.21.6.', environments + 'paper-1.21.6.json', java21, -1)
createTestTask('luskTest_1_21_7', 'Runs tests on Paper 1.21.7.', environments + 'paper-1.21.7.json', java21, -1)
createTestTask('luskTest_1_21_8', 'Runs tests on Paper 1.21.8.', environments + 'paper-1.21.8.json', java21, -1)
createTestTask('luskTest_1_21_9', 'Runs tests on Paper 1.21.9.', environments + 'paper-1.21.9.json', java21, -1)
createTestTask('luskTest_1_21_10', 'Runs tests on Paper 1.21.10.', environments + 'paper-1.21.10.json', java21, -1)

tasks.register("luskTestOlderSkriptVersions") {
    description = 'Runs tests on older skript versions.'
    dependsOn luskTest_1_16_5_skript_2_6_4, luskTest_1_16_5_skript_2_7_3, luskTest_1_16_5_skript_2_8_7
}

tasks.register('luskTest') {
    description = 'Runs tests on all environments.'
    dependsOn luskTestOlderSkriptVersions, luskTest_1_16_5, luskTest_1_17_1, luskTest_1_18_2, luskTest_1_19_4, luskTest_1_20_6, luskTest_1_21_4, luskTest_1_21_5, luskTest_1_21_6, luskTest_1_21_7, luskTest_1_21_8, luskTest_1_21_9, luskTest_1_21_10
}
